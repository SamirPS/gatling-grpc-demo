plugins {
  id 'idea'
  id 'java'
  id 'com.google.protobuf'   version '0.9.3'
  id 'com.diffplug.spotless' version '6.18.0'
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'io.grpc:grpc-netty-shaded:1.54.1' // shaded: includes ssl libraries
  implementation 'io.grpc:grpc-protobuf:1.54.1'
  implementation 'io.grpc:grpc-services:1.54.1' // reflection
  implementation 'io.grpc:grpc-stub:1.54.1'
}

idea {
  module {
    sourceDirs += file("${buildDir}/generated/source/proto/main/java");
    sourceDirs += file("${buildDir}/generated/source/proto/main/grpc");
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.22.3"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.54.1'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

spotless {
  java {
    googleJavaFormat().aosp()
    //importOrder = Seq("java", "javax", "scala", "io.gatling", "", "\\#")
    //importOrder('java|javax', 'com.acme', '', '\\#com.acme', '\\#')
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include '**/*.java'
      exclude 'build/generated/**/*.*'
    }
  }
}
